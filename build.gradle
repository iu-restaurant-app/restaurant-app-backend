plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.1'
	id 'io.spring.dependency-management' version '1.1.5'
}

group = 'iu.frontenders'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = JavaVersion.VERSION_20
    targetCompatibility = JavaVersion.VERSION_20
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation("io.minio:minio:8.5.11")
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	runtimeOnly 'org.postgresql:postgresql'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.register("stopDockerImage", Exec) {
	group "docker"
	commandLine "docker", "compose", "down"
}

bootJar {
	archiveFileName = "restaurantAppBackend.jar"
}

tasks.register("bootJarWithStop") {
	group "build"
	dependsOn bootJar
	dependsOn stopDockerImage
}

tasks.register("buildDeployDockerImage", Exec) {
	group "docker"
	dependsOn bootJar
	commandLine "docker", "build", "--platform", "linux/amd64", "-t", "almaxgood/restaurant-app-backend", "."
}

tasks.register("buildDockerImage", Exec) {
	group "docker"
	dependsOn bootJar
	commandLine "docker", "build", "-t", "restaurant-app-backend", "."
}

tasks.register("rebuildDockerImage", Exec) {
	group "docker"
	dependsOn bootJarWithStop
	commandLine "docker", "build", "-t", "restaurant-app-backend", "."
}

tasks.register("startDockerImage", Exec) {
	group "docker"
	dependsOn buildDockerImage
	commandLine "docker", "compose", "up", "-d"
}

tasks.register("restartDockerImage", Exec) {
	group "docker"
	dependsOn rebuildDockerImage
	commandLine "docker", "compose", "up", "-d"
}
